///////////////////////////////////////////
////////// 橘花养成功能辞书
////////// written by forjane 2009.09.21
///////////////////////////////////////////

//需要lunar.dic,clock.dic和aya_communicate.dic的部分功能
//------------------------------------------------------

#define				RPGPATH		mydata\rpg


KikkaVar
{
	kikkaexpmax = kikkalvl * 20 + 20;
	kikkapowmax = kikkalvl * 5 + 5;
	kikkawizmax = kikkapowmax;
	kikkachmmax = kikkapowmax;
	kikkastmmax = kikkalvl * 20 + 220;
}


KikkaMenu
{
	if showkikka {
		"\_l[0,210]" + ColorText("\__q[OnKikkaMenu,0]\f[cursorstyle,none]─────────── 橘花养成系统↓\f[cursorstyle,default]\__q\n[140]","color=160,160,200");
		--
		"\_l[82]\![*]\q[购买物品,OnKikkaItemList,0]  \![*]\q[日程安排,OnKikkaClass,%(weekday)]\n";
		--
//		"\_l[176,347]\q[Reset,OnKikkaReset]"
		--
		KikkaStatus;
	}
	else {
		"\_l[0,332]" + ColorText("\__q[OnKikkaMenu,1]\f[cursorstyle,none]─────────── 橘花养成系统↑\f[cursorstyle,default]\__q\n","color=160,160,200");
		--
		KikkaInfo;
	}
}


OnKikkaMenu
{
	showkikka = reference0;
	OpenMenu(0);
}



KikkaStatusChange
{
	_ref = _argv[0];
	if ISVAR("kikkaexp") == 0 {
		OnKikkaReset;
	}
	if "exp=" _in_ _ref {
		_tmp = _ref[1,"exp="];
		kikkaexp += EVAL(_tmp[0]);
		if kikkaexp >= kikkaexpmax {
			kikkalvl ++;
			kikkaexp -= kikkaexpmax;
			KikkaVar;
		}
	}
	if "pow=" _in_ _ref {
		_tmp = _ref[1,"pow="];
		kikkapow += EVAL(_tmp[0]);
		if kikkapow > kikkapowmax {
			kikkapow = kikkapowmax;
		}
		elseif kikkapow < 0 {
			kikkapow = 0;
		}
	}
	if "wiz=" _in_ _ref {
		_tmp = _ref[1,"wiz="];
		kikkawiz += EVAL(_tmp[0]);
		if kikkawiz > kikkawizmax {
			kikkawiz = kikkawizmax;
		}
		elseif kikkawiz < 0 {
			kikkawiz = 0;
		}
	}
	if "chm=" _in_ _ref {
		_tmp = _ref[1,"chm="];
		kikkachm += EVAL(_tmp[0]);
		if kikkachm > kikkachmmax {
			kikkachm = kikkachmmax;
		}
		elseif kikkachm < 0 {
			kikkachm = 0;
		}
	}
	if "stm=" _in_ _ref {
		_tmp = _ref[1,"stm="];
		kikkastm += EVAL(_tmp[0]);
		if kikkastm > kikkastmmax {
			kikkastm = kikkastmmax;
		}
		elseif kikkastm < 0 {
			kikkastm = 0;
		}
	}
	if "money=" _in_ _ref {
		_tmp = _ref[1,"money="];
		kikkamoney += EVAL(_tmp[0]);
	}
	if "love=" _in_ _ref {
		_tmp = _ref[1,"love="];
		intimacy += EVAL(_tmp[0]);
	}
}


KikkaInfo
{
	Lunar;
	_ampm = TimeName;
	_color = "default";
	_hostname=FUNCTIONEX("saori\saori_ip.dll","host");
	_ipaddress=FUNCTIONEX("saori\saori_ip.dll","ip");
	_tmp = FOPEN("var/ip.php","r");
	_ipaddress2 = FREAD("var/ip.php");
	FCLOSE("var/ip.php");
	if _ipaddress2 == -1 || _ipaddress2 == "" || '<' _in_ _ipaddress2 {
		_ipaddress2 = "未知";
	}

	if weekday == 0 || weekday == 6 {
		_color = "238,50,95";
	}
	"\_l[0,204]\f[bold,true]系统资讯 ----\f[bold,default]\n[150]/
	%(year)年%(month)月%(day)日  \f[color,%(_color)]星期%(lunardata[3])\f[color,default]   %(_ampm)%(hour12)点%(minute)分\n/
	农历 %(lunardata[4])年 %(lunardata[5])月%(lunardata[6])    \f[color,238,50,95]%(lunardata[10])\f[color,default]\n[150]/
	系统启动：%(systemuphour)小时%(systemupminute)分\n/
	人格启动：%(ghostuptime/3600)小时%(ghostuptime/60%60)分\n/
	内存使用：%(memoryload)\%\n/
	内网IP：%(_ipaddress)\n/
	外网IP：%(_ipaddress2) \_l[146]\__q[OnRefleshKikkaInfo]\[查询外网IP\]\__q";
}

OnRefleshKikkaInfo
{
	"\b[2]\![execute,http-get,http://usada.sakura.vg/ip.php,--param-charset=UTF-8,--timeout=50,--async=kikkagetip]"
}

KikkaStatus
{
	KikkaVar;
	_txt ="";
	_txt += "\_l[0,226]";
	_txt += ColorText("等级：%(kikkalvl)","bold,height=16,name=Arial,color=%(ColorIdx(6))");
	_txt += ColorText("\n[150]经验：","bold,color=%(ColorIdx(3))") + ColorBar(3,42,kikkaexp,kikkaexpmax,,"colortxt=62");
	_txt += ColorText("\n身手：","bold,color=%(ColorIdx(9))") + ColorBar(9,42,kikkapow,kikkapowmax,,"colortxt=62");
	_txt += ColorText("\n学识：","bold,color=%(ColorIdx(4))") + ColorBar(4,42,kikkawiz,kikkawizmax,,"colortxt=62");
	_txt += ColorText("\n魅力：","bold,color=%(ColorIdx(5))") + ColorBar(5,42,kikkachm,kikkachmmax,,"colortxt=62");
	_txt += ColorText("\n体力：","bold,color=%(ColorIdx(1))") + ColorBar(1,42,kikkastm,kikkastmmax,,"colortxt=62");
	_txt += ColorText("\n好感度：%(intimacy)","bold,color=%(ColorIdx(6))");
	_heart = "\_l[103]";
	for _i = TOINT(POW(intimacy,0.39208)) / 3; _i > 0; _i -- {
		_heart += "\_b[RPGPATH\heart2.gif,inline]";
	}
	for _i = TOINT(POW(intimacy,0.39208)) % 3; _i > 0; _i -- {
		_heart += "\_b[RPGPATH\heart1.gif,inline]";
	}
	_txt += _heart;
	_txt += ColorText("\n金钱：￥%(kikkamoney)","bold,color=244,190,0");
	_txt;
}

KikkaGrow
{
	kikkatick ++;
	KikkaStatusChange("exp=1");
	_class = kikkaclass[weekday];
	_lvl = kikkalvl / 30 + 1;

	if kikkatick % 5 == 0 && _class != "休息" {
		KikkaStatusChange("stm=-%(_lvl)");
		if kikkastm == 0 {
			kikkaangry ++;
			"\c\0\s[3]橘花好饿喔，\w9有什么东西吃吗……\w9";
			if kikkaangry >= 12 {
				kikkaangry = 0;
				intimacy --;
			}
		}
		else {
			kikkaangry = 0;
		}
	}
	if kikkatick % 30 == 0 && kikkastm > 0 && _class != "休息" {
		case _class {
			when "枪械" {
				KikkaStatusChange("pow=%(3 * _lvl)");
			}
			when "语言" {
				KikkaStatusChange("wiz=%(3 * _lvl)");
			}
			when "艺术" {
				KikkaStatusChange("chm=%(3 * _lvl)");
			}
			when "外卖" {
				KikkaStatusChange("pow=-%(_lvl),money=%(kikkapow)");
			}
			when "翻译" {
				KikkaStatusChange("wiz=-%(_lvl),money=%(kikkawiz)");
			}
			when "广告" {
				KikkaStatusChange("chm=-%(_lvl),money=%(kikkachm)");
			}
		}
	}
}

OnKikkaReset
{
	kikkalvl =1;
	kikkaexp = 0;
	kikkapow = 0;
	kikkawiz = 0;
	kikkachm = 0;
	kikkastm = 240;
	kikkamoney = 1000;
	kikkatick = 0;
//	intimacy = 0;
	kikkaclass = ("休息","休息","休息","休息","休息","休息","休息");
//	OpenMenu(0);
}

OnKikkaItemList
{
	KikkaItemTalk(reference0);
	--
	"\b[2]\n\n\_q食物：\n/
	\![*]\__q[OnKikkaItem,2,-300,stm=150,exp=5]馒头 \_l[64]￥300 \_l[108]体力 150 \_l[168]经验 5\__q\n/
	\![*]\__q[OnKikkaItem,3,-600,stm=300,exp=10]鸡腿汉堡 \_l[64]￥600 \_l[108]体力 300 \_l[168]经验 10\__q\n/
	\![*]\__q[OnKikkaItem,4,-1200,stm=600,exp=20]花式蛋糕 \_l[64]￥1200 \_l[108]体力 600 \_l[168]经验 20\__q\n/
	\![*]\__q[OnKikkaItem,5,-2000,stm=1000,exp=40]龙虾大餐 \_l[64]￥2000 \_l[108]体力 1000 \_l[168]经验 40\__q\n"
	--
	"礼物：\n/
	\![*]\__q[OnKikkaItem,6,-1250,pow=4,love=1]电锯 \_l[64]￥1250 \_l[118]身手 4 \_l[168]好感 1\__q\n/
	\![*]\__q[OnKikkaItem,7,-6000,pow=20,love=5]P90 \_l[64]￥6000 \_l[118]身手 20 \_l[168]好感 5\__q\n/
	\![*]\__q[OnKikkaItem,8,-1250,wiz=4,love=1]派克笔 \_l[64]￥1250 \_l[118]学识 4 \_l[168]好感 1\__q\n/
	\![*]\__q[OnKikkaItem,9,-6000,wiz=20,love=5]电脑 \_l[64]￥6000 \_l[118]学识 20 \_l[168]好感 5\__q\n/
	\![*]\__q[OnKikkaItem,10,-1250,chm=4,love=1]香水 \_l[64]￥1250 \_l[118]魅力 4 \_l[168]好感 1\__q\n/
	\![*]\__q[OnKikkaItem,11,-6000,chm=20,love=5]钻戒 \_l[64]￥6000 \_l[118]魅力 20 \_l[168]好感 5\__q\n"
	--
	"\n[half]\q[◇返回,OnKikkaMenu,1]    \q[◇终了,OnKikkaEnd]"
	--
	KikkaStatus;
}

KikkaItemTalk
{
	_tmp = _argv[0];
	("\0\s[40]\b[2]\要买什么东西给橘花呢？",/
	"\0\s[8]\b[2]哎……钱不够呀！！\w9",/
	"\0\s[3]\b[2]嗯……\w9嗯，好难吃啊……\w9\1要不要喝水啊？\0",/
	"\0\s[5]\b[2]嗯……\w9好好吃哦~~\w9\1真的么？\w9难道是肯德基？\0",/
	"\0\s[5]\b[2]是草莓味的，\w9还是巧克力的？……\w9好好吃~",/
	"\0\s[5]\b[2]嗯……\w9嗯……\w9吃的好饱啊~~\w9\1有鱼吗？\0",/
	"\0\s[27]\b[2]好沉……\w9\1\s[11]喂喂，别乱挥呀！\0",/
	"\0\s[22]\b[2]新玩具？\w9给我的吗？\w9我保证……\1\s[13]不要用XT002的口气说话= =\0",/
	"\0\s[2]\b[2]好贵的钢笔……\w9",/
	"\0\s[5]\b[2]电脑正好需要升级了，\w9谢谢%(username)^^\w9\1才刚用过没多久吧……\0",/
	"\0\s[26]\b[2]哇……\w9是我喜欢的香味呢……\w9",/
	"\0\s[28]\b[2]好漂亮……\w9那个…\w9谢谢你……\w9",/
	)[_tmp];
}

OnKikkaItem
{
	if reference1 + kikkamoney < 0 {
		reference0 = 1;
	}
	else {
		KikkaStatusChange("money=%(reference1),%(reference2),%(reference3)");
	}		
	--
	OnKikkaItemList;
}


OnKikkaClass
{
	_txt = "";
	_day = reference0;
	_class = kikkaclass;
	_class[_day] = ColorText(_class[_day],"color=50,80,225");
	for _i = 0; _i < 7; _i ++ {
		_txt += "\_l[%(_i * 32)]" + _class[(_i + 1) % 7];
	}
	_lvl = kikkalvl / 30 + 1;
	_txt = "\0\b[2]\_q安排橘花的日程：\n\n━━━━━━━━━━━━━━━━━━\n/
	周一  周二  周三  周四  周五  \f[color,238,50,95]周六  周日\f[color,default]\n/
	──────────────────\n" + _txt + "\n━━━━━━━━━━━━━━━━━━";
	_txt += "\n\n课程：\n/
			\![*]\__q[OnKikkaClassSet,%(_day),枪械]枪械课(lv.%(_lvl))\_l[86]身手 +%(_lvl * 3)\__q\n/
			\![*]\__q[OnKikkaClassSet,%(_day),语言]语言课(lv.%(_lvl))\_l[86]学识 +%(_lvl * 3)\__q\n/
			\![*]\__q[OnKikkaClassSet,%(_day),艺术]艺术课(lv.%(_lvl))\_l[86]魅力 +%(_lvl * 3)\__q\n/
			打工：\n/
			\![*]\__q[OnKikkaClassSet,%(_day),外卖]送外卖(lv.%(_lvl))\_l[86]身手 %(0 - _lvl)\_l[134]薪水=身手值\__q\n/
			\![*]\__q[OnKikkaClassSet,%(_day),翻译]做翻译(lv.%(_lvl))\_l[86]学识 %(0 - _lvl)\_l[134]薪水=学识值\__q\n/
			\![*]\__q[OnKikkaClassSet,%(_day),广告]拍广告(lv.%(_lvl))\_l[86]魅力 %(0 - _lvl)\_l[134]薪水=魅力值\__q\n/
			休息：\n/
			\![*]\__q[OnKikkaClassSet,%(_day),休息]休息\_l[50]体力不会随时间下降\__q\n"
	_txt += "\n[150]\q[◇返回,OnKikkaMenu,1]    \q[◇终了,OnKikkaEnd]"
	_txt;
}

OnKikkaClassSet
{
	kikkaclass[reference0] = reference1;
	reference0 = (reference0 + 1) % 7;
	OnKikkaClass;
}

ColorIdx
{
	_coloridx = "|93,188,91|194,243,99|108,129,182|165,203,214|216,67,179|238,50,95|255,197,52|216,233,40|229,134,82|231,171,109";
	_coloridx[_argv[0],'|'];
}


ColorText
{
	_txt = _argv[0];
	_mode = _argv[1];
	if "bold" _in_ _mode {
		_txt = "\f[bold,true]" + _txt + "\f[bold,default]";
	}
	if "italic" _in_ _mode {
		_txt = "\f[italic,true]" + _txt + "\f[italic,default]";
	}
	if "name" _in_ _mode {
		_tmp = _mode[1,"name="];
		_tmp = _tmp[0];
		_txt = "\f[name,%(_tmp)]" + _txt + "\f[name,default]";
	}
	if "height" _in_ _mode {
		_tmp = _mode[1,"height="];
		_tmp = _tmp[0];
		_txt = "\f[height,%(_tmp)]" + _txt + "\f[height,default]";
	}
	if "color" _in_ _mode {
		_tmp = _mode[1,"color="];
		_tmp = _tmp[0] + "," + _tmp[1] + "," + _tmp[2];
		_txt = "\f[color,%(_tmp)]" + _txt + "\f[color,default]";
	}
	_txt;
}


//ColorBar(_Color,_XPos,_Num,_MaxNum{default=100},_MaxLen{default=100},_Mode{"txt=textwidth,colortxt=textwidth,nobg"})
//----------------------------------------------------------------------------------------------------------------------------
ColorBar
{
	_color = _argv[0];
	_xpos = _argv[1];
	_maxlen = 100;
	_percent = _argv[2];
	_max = 100;
	_mode = _argv[5];
	_txt = "\_l[%(_xpos)]";
	_len = 0;
	if _argv[3] != "" {
		_max = _argv[3];
	}
	if _argv[4] != "" {
		_maxlen = _argv[4];
	}
	if "txt" _in_ _mode {
		_txt += "%(_percent)/%(_max)";
		_tmp = 0;
		if "txt=" _in_ _mode {
			_tmp = EVAL(_mode[1,'txt=']) + _xpos;
		}
		else {
			_tmp = 14 * STRLEN(TOSTR(_max)) + 6 + _xpos;
		}
		_txt +="\_l[%(_tmp)]";
	}
	if "colortxt" _in_ _mode {
		_colorstr = "|93,188,91|194,243,99|108,129,182|165,203,214|216,67,179|238,50,95|255,197,52|216,233,40|229,134,82|231,171,109";
		_txt = "\f[color,%(_colorstr[_color,'|'])]" + _txt + "\f[color,default]";
	}
	_length = _percent * _maxlen / _max;
	_txt += "\_b[RPGPATH\colorbar.bmp,inline,--option=opaque,--clipping=0 %(_color * 10) 2 %(_color * 10 + 10)]";
	if _length > 0 {
		if _length > _maxlen {
			_length = _maxlen;
		}
		_txt += "\_b[RPGPATH\colorbar.bmp,inline,--option=opaque,--clipping=%(159 - _length) %(_color * 10) 159 %(_color * 10 + 10)]";
	}
	_txt += "\_b[RPGPATH\colorbar.bmp,inline,--option=opaque,--clipping=159 %(_color * 10) 160 %(_color * 10 + 10)]";
	if _length < _maxlen && "nobg" !_in_ _mode {
		_txt += "\_b[RPGPATH\colorbar.bmp,inline,--option=opaque,--clipping=0 0 %(_maxlen - _length) 10]";
	}
	_txt;
}

